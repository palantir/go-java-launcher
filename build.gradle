/*
 * (c) Copyright 2021 Palantir Technologies Inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.2.1'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.28.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.12.3'
    }
}

apply plugin: 'com.palantir.external-publish-dist'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

group 'com.palantir.launching'
version gitVersion()

task distTar(type: Tar) {
    compression Compression.GZIP

    into ('bin') {
        from 'dist'
    }

    ['x86_64-unknown-linux-musl', 'x86_64-apple-darwin', 'x86_64-pc-windows-msvc'].each { target -> 
        into ("bin/$target") {
            from "target/$target/release/conjure-rust"
            from "target/$target/release/conjure-rust.exe"
        }
    }

    into archiveFileName.map { s -> s[0..<s.lastIndexOf('.')] }
}
